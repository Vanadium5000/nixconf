{ self, ... }:
{
  flake.nixosModules.firefox =
    {
      config,
      lib,
      pkgs,
      ...
    }:
    let
      inherit (self) colorsRgbaValues;
      inherit (lib)
        mkOption

        types
        mkIf
        mkMerge
        mapAttrsToList
        ;

      cfg = config.programs.librewolf;
      user = config.preferences.user.username;

      # Helpers
      jsonFormat = pkgs.formats.json { };

      ext = name: "https://addons.mozilla.org/firefox/downloads/latest/${name}/latest.xpi";

      mkColor = color: {
        r = builtins.elemAt colorsRgbaValues.${color} 0;
        g = builtins.elemAt colorsRgbaValues.${color} 1;
        b = builtins.elemAt colorsRgbaValues.${color} 2;
      };

      mkUserJs = prefs: extraPrefs: ''
        // Generated by NixOS Module
        ${lib.concatStrings (
          mapAttrsToList (name: value: ''
            user_pref("${name}", ${builtins.toJSON value});
          '') prefs
        )}
        ${extraPrefs}
      '';

      # Persistence configuration using bind mount for reliability
      permissionsPersistence = self.lib.persistence.mkPersistent {
        method = "bind";
        inherit user;
        fileName = "librewolf-permissions.sqlite";
        targetFile = "/home/${user}/.librewolf/${user}.default/permissions.sqlite";
      };

      # Profile submodule
      profileModule = types.submodule (
        { config, name, ... }:
        {
          options = {
            id = mkOption {
              type = types.int;
              default = 0;
              description = "Profile ID.";
            };
            path = mkOption {
              type = types.str;
              default = name;
              description = "Profile folder name.";
            };
            isDefault = mkOption {
              type = types.bool;
              default = config.id == 0;
              description = "Is this the default profile?";
            };
            settings = mkOption {
              type = types.attrsOf (
                types.oneOf [
                  types.bool
                  types.int
                  types.str
                  types.float
                ]
              );
              default = { };
              description = "Preferences for user.js.";
            };
            extraConfig = mkOption {
              type = types.lines;
              default = "";
              description = "Extra lines for user.js.";
            };
            extensions = mkOption {
              default = { };
              type = types.submodule {
                options = {
                  packages = mkOption {
                    type = types.listOf types.package;
                    default = [ ];
                    description = "List of extension packages to install (declarative).";
                  };
                  settings = mkOption {
                    type = types.attrsOf (
                      types.submodule {
                        options = {
                          settings = mkOption {
                            type = types.attrsOf jsonFormat.type;
                            default = { };
                            description = "Internal storage data for the extension (storage.js).";
                          };
                        };
                      }
                    );
                    default = { };
                    description = "Settings for specific extensions by ID.";
                  };
                };
              };
            };
          };
        }
      );

    in
    {
      options.programs.librewolf = {
        enable = mkOption {
          type = types.bool;
          default = true;
          description = "Whether to enable LibreWolf Browser.";
        };

        package = mkOption {
          type = types.package;
          default = pkgs.librewolf;
          description = "The package to use.";
        };

        policies = mkOption {
          type = types.attrsOf jsonFormat.type;
          default = { };
          description = "Policies (policies.json).";
        };

        profiles = mkOption {
          type = types.attrsOf profileModule;
          default = { };
          description = "Profiles configuration.";
        };
      };

      config = mkIf cfg.enable {
        # 1. Install the package with policies
        environment.systemPackages = [
          (cfg.package.override {
            extraPolicies = cfg.policies;
          })
        ];

        # Persistence
        impermanence.home.cache.directories = [
          # ".mozilla"
          ".librewolf"
        ];

        # Configure Profiles via hjem
        hjem.users.${user}.files = mkMerge (
          lib.flatten (
            lib.mapAttrsToList (
              profileName: profile:
              let
                profileDir = ".librewolf/${profile.path}";
                # Extension settings need ExtensionStorageIDB disabled to work via storage.js
                storageSettings = profile.extensions.settings;
                hasStorageSettings = storageSettings != { };

                finalPrefs =
                  profile.settings
                  // (
                    if hasStorageSettings then
                      { "extensions.webextensions.ExtensionStorageIDB.enabled" = false; }
                    else
                      { }
                  );

                # Prepare extension packages (symlink)
                extensionsEnv =
                  if profile.extensions.packages != [ ] then
                    pkgs.buildEnv {
                      name = "hm-firefox-extensions-${profileName}";
                      paths = profile.extensions.packages;
                    }
                  else
                    null;

              in
              [
                # user.js
                {
                  "${profileDir}/user.js".text = mkUserJs finalPrefs profile.extraConfig;
                }

                # Extensions installation (symlink)
                (mkIf (extensionsEnv != null) {
                  "${profileDir}/extensions" = {
                    source = "${extensionsEnv}/share/mozilla/extensions/{ec8030f7-c20a-464f-9b0e-13a3a9e97384}";
                    recursive = true;
                  };
                })

                # Extension Settings (storage.js)
                (mkMerge (
                  mapAttrsToList (
                    extId: extCfg:
                    mkIf (extCfg.settings != { }) {
                      "${profileDir}/browser-extension-data/${extId}/storage.js" = {
                        text = builtins.toJSON extCfg.settings;
                      };
                    }
                  ) storageSettings
                ))
              ]
            ) cfg.profiles
          )
          ++ [
            # profiles.ini
            {
              ".librewolf/profiles.ini".text = lib.generators.toINI { } (
                {
                  General = {
                    StartWithLastProfile = 1;
                    Version = 2;
                  };
                }
                // (lib.mapAttrs' (
                  name: profile:
                  lib.nameValuePair "Profile${toString profile.id}" {
                    Name = name;
                    Path = profile.path;
                    IsRelative = 1;
                    Default = if profile.isDefault then 1 else 0;
                  }
                ) cfg.profiles)
              );
            }
          ]
        );

        system.activationScripts.firefox-permissions = {
          text = permissionsPersistence.activationScript;
          deps = [ "users" ];
        };

        # Bind mount for reliable persistence (apps can't overwrite)
        fileSystems = permissionsPersistence.fileSystems;

        # Default Configuration
        programs.librewolf.policies = {
          DisableTelemetry = true;
          DisableFirefoxStudies = true;
          EnableTrackingProtection = {
            Value = true;
            Locked = true;
            Cryptomining = true;
            Fingerprinting = true;
          };
          ExtensionSettings = {
            # "uBlock0@raymondhill.net" = { installation_mode = "force_installed"; ... };
          };
          Extensions.Install = map ext [
            "ublock-origin"

            # Bookmarks & tabs sync across browsers any WebDAV or Git service,
            # via local file, Nextcloud, or Google Drive
            # "floccus"

            # Dark mode for every website
            "darkreader"

            # YouTube enhancements
            "sponsorblock"
            "return-youtube-dislikes"
            #youtube-enhancer-vc # Block YT shorts & general improvements

            "tampermonkey" # Userscripts
            "private-grammar-checker-harper" # Private spellcheck

            "firefox-color" # Base 16 theming

            "multi-account-containers" # Firefox Multi-account Containers
            "container-proxy" # Proxies for Firefox Multi-account Containers
          ];
          SearchEngines = {
            Default = "StartPage";
          };
        };

        programs.librewolf.profiles.${user} = {
          id = 0;
          isDefault = true;
          path = "${user}.default";

          /*
            * [NOTE] Cookie Clear Exceptions: For cross-domain logins, add exceptions for both sites
            * e.g. https://www.youtube.com (site) + https://accounts.google.com (single sign on)
            * [WARNING] Be selective with what sites you "Allow", as they also disable partitioning (1767271)
            * [SETTING] to add site exceptions: Ctrl+I>Permissions>Cookies>Allow (when on the website in question)
            * [SETTING] to manage site exceptions: Options>Privacy & Security>Permissions>Settings **
          */
          settings = {
            "sidebar.verticalTabs" = true;
            "sidebar.visibility" = "expand-on-hover";
            "layout.spellcheckDefault" = 0; # Disable spellcheck - use Harper spellcheck instead
            "widget.gtk.overlay-scrollbars.enabled" = false; # Always show scrollbars
            "layout.css.always_underline_links" = true;

            # Firefox Sync
            "identity.fxaccounts.enabled" = true; # Enable Firefox Sync for Bookmarks & Account Containers (requires syncing addons)
            "services.sync.declinedEngines" = "prefs,passwords,tabs,addresses,creditcards,forms,history"; # Don't sync these

            # Don't suggest stuff in the urlbar
            "browser.urlbar.suggest.history" = false;

            # Enable support for local proxies
            "network.proxy.allow_hijacking_localhost" = true;

            # Re-enable browser rendering stuff
            "webgl.disabled" = false;

            # Disable annoying swipe gestures (alt + left/right arrow is so much easier)
            "browser.gesture.swipe.left" = "";
            "browser.gesture.swipe.right" = "";

            # Dark theme for websites
            "layout.css.prefers-color-scheme.content-override" = 0;

            # Fix the annoying preview on links on long press
            "browser.ml.linkPreview.longPress" = false;

            # TODO: Swap to Firefox Colors
            # Default Firefox dark browser theme
            "browser.theme.content-theme" = 0;
            "browser.theme.toolbar-theme" = 0;
            "extensions.activeThemeID" = "firefox-compact-dark@mozilla.org";

            # Disable AV1 playback for "macbook" as it is too graphics-intensive for non-accelerated hardware
            "media.av1.enabled" = if config.preferences.hostName == "macbook" then false else true;
          };

          # Firefox Color Settings
          extensions.settings."FirefoxColor@mozilla.com".settings = {
            firstRunDone = true;
            theme = {
              title = "Stylix Librewolf";
              images.additional_backgrounds = [ "./bg-000.svg" ];
              colors = {
                toolbar = mkColor "base00";
                toolbar_text = mkColor "base05";
                frame = mkColor "base01";
                tab_background_text = mkColor "base05";
                toolbar_field = mkColor "base02";
                toolbar_field_text = mkColor "base05";
                tab_line = mkColor "base0D";
                popup = mkColor "base00";
                popup_text = mkColor "base05";
                button_background_active = mkColor "base04";
                frame_inactive = mkColor "base00";
                icons_attention = mkColor "base0D";
                icons = mkColor "base05";
                ntp_background = mkColor "base00";
                ntp_text = mkColor "base05";
                popup_border = mkColor "base0D";
                popup_highlight_text = mkColor "base05";
                popup_highlight = mkColor "base04";
                sidebar_border = mkColor "base0D";
                sidebar_highlight_text = mkColor "base05";
                sidebar_highlight = mkColor "base0D";
                sidebar_text = mkColor "base05";
                sidebar = mkColor "base00";
                tab_background_separator = mkColor "base0D";
                tab_loading = mkColor "base05";
                tab_selected = mkColor "base00";
                tab_text = mkColor "base05";
                toolbar_bottom_separator = mkColor "base00";
                toolbar_field_border_focus = mkColor "base0D";
                toolbar_field_border = mkColor "base00";
                toolbar_field_focus = mkColor "base00";
                toolbar_field_highlight_text = mkColor "base00";
                toolbar_field_highlight = mkColor "base0D";
                toolbar_field_separator = mkColor "base0D";
                toolbar_vertical_separator = mkColor "base0D";
              };
            };
          };
        };
      };
    };
}
