{ self, ... }:
{
  flake.nixosModules.firefox =
    {
      pkgs,
      lib,
      config,
      ...
    }:
    let
      inherit (self) colorsRgbaValues;
      ext = name: "https://addons.mozilla.org/firefox/downloads/latest/${name}/latest.xpi";

      extensions = extensionStrings: (builtins.map ext extensionStrings);

      mkOverridesFile = prefs: ''
        // Generated by Home Manager.

        ${lib.concatStrings (
          lib.mapAttrsToList (name: value: ''
            pref("${name}", ${builtins.toJSON value});
          '') prefs
        )}
      '';

      mkColor = color: {
        r = builtins.elemAt colorsRgbaValues.${color} 0;
        g = builtins.elemAt colorsRgbaValues.${color} 1;
        b = builtins.elemAt colorsRgbaValues.${color} 2;
      };

      user = config.preferences.user.username;

      /*
          * [NOTE] Cookie Clear Exceptions: For cross-domain logins, add exceptions for both sites
          * e.g. https://www.youtube.com (site) + https://accounts.google.com (single sign on)
          * [WARNING] Be selective with what sites you "Allow", as they also disable partitioning (1767271)
          * [SETTING] to add site exceptions: Ctrl+I>Permissions>Cookies>Allow (when on the website in question)
          * [SETTING] to manage site exceptions: Options>Privacy & Security>Permissions>Settings **
      */
      settings = {
        "sidebar.verticalTabs" = true;
        "sidebar.visibility" = "expand-on-hover";
        "layout.spellcheckDefault" = 0; # Disable spellcheck - use Harper spellcheck instead
        "widget.gtk.overlay-scrollbars.enabled" = false; # Always show scrollbars
        "layout.css.always_underline_links" = true;

        # Firefox Sync
        "identity.fxaccounts.enabled" = true; # Enable Firefox Sync for Bookmarks & Account Containers
        "services.sync.declinedEngines" =
          "prefs,passwords,tabs,addons,addresses,creditcards,forms,history"; # Don't sync these

        # Don't suggest stuff in the urlbar
        "browser.urlbar.suggest.history" = false;

        # Re-enable browser rendering stuff
        "webgl.disabled" = false;

        # Disable annoying swipe gestures (alt + left/right arrow is so much easier)
        "browser.gesture.swipe.left" = "";
        "browser.gesture.swipe.right" = "";

        # Dark theme for websites
        "layout.css.prefers-color-scheme.content-override" = 0;

        # TODO: Swap to Firefox Colors
        # Default Firefox dark browser theme
        "browser.theme.content-theme" = 0;
        "browser.theme.toolbar-theme" = 0;
        "extensions.activeThemeID" = "firefox-compact-dark@mozilla.org";

        # Disable AV1 playback as it is too graphics-intensive for non-accelerated hardware
        # "media.av1.enabled" = false;
      };

    in
    {
      # Add the Librewolf package
      environment.systemPackages = [
        (pkgs.librewolf.override {
          extraPolicies = {
            Preferences = settings;
            Extensions.Install = extensions [
              # Ad-block
              "ublock-origin"

              # Bookmarks & tabs sync across browsers any WebDAV or Git service,
              # via local file, Nextcloud, or Google Drive
              # "floccus"

              # Dark mode for every website
              "darkreader"

              # YouTube enhancements
              "sponsorblock"
              "return-youtube-dislikes"
              #youtube-enhancer-vc # Block YT shorts & general improvements

              "tampermonkey" # Userscripts
              "private-grammar-checker-harper" # Private spellcheck

              "firefox-color" # Base 16 theming

              "multi-account-containers" # Firefox Multi-account Containers
            ];
            SearchEngines.Default = "StartPage";

            # Manage extension settings
            # FIXME: This has no effect for some reason
            "3rdparty".Extensions = {
              "FirefoxColor@mozilla.com" = {
                firstRunDone = true;
                theme = {
                  title = "Stylix Librewolf";
                  colors = {
                    toolbar = mkColor "base00";
                    toolbar_text = mkColor "base05";
                    frame = mkColor "base01";
                    tab_background_text = mkColor "base05";
                    toolbar_field = mkColor "base02";
                    toolbar_field_text = mkColor "base05";
                    tab_line = mkColor "base0D";
                    popup = mkColor "base00";
                    popup_text = mkColor "base05";
                    button_background_active = mkColor "base04";
                    frame_inactive = mkColor "base00";
                    icons_attention = mkColor "base0D";
                    icons = mkColor "base05";
                    ntp_background = mkColor "base00";
                    ntp_text = mkColor "base05";
                    popup_border = mkColor "base0D";
                    popup_highlight_text = mkColor "base05";
                    popup_highlight = mkColor "base04";
                    sidebar_border = mkColor "base0D";
                    sidebar_highlight_text = mkColor "base05";
                    sidebar_highlight = mkColor "base0D";
                    sidebar_text = mkColor "base05";
                    sidebar = mkColor "base00";
                    tab_background_separator = mkColor "base0D";
                    tab_loading = mkColor "base05";
                    tab_selected = mkColor "base00";
                    tab_text = mkColor "base05";
                    toolbar_bottom_separator = mkColor "base00";
                    toolbar_field_border_focus = mkColor "base0D";
                    toolbar_field_border = mkColor "base00";
                    toolbar_field_focus = mkColor "base00";
                    toolbar_field_highlight_text = mkColor "base00";
                    toolbar_field_highlight = mkColor "base0D";
                    toolbar_field_separator = mkColor "base0D";
                    toolbar_vertical_separator = mkColor "base0D";
                  };
                };
              };
            };
          };
        })
      ];

      hjem.users.${user}.files = {
        ".librewolf/librewolf.overrides.cfg".text = mkOverridesFile settings;
        ".librewolf/profiles.ini".text = ''
          [Profile0]
          Name=${config.preferences.user.username}
          IsRelative=1
          Path=${config.preferences.user.username}.default
          Default=1

          [General]
          StartWithLastProfile=1
          Version=2'';
        ".librewolf/${config.preferences.user.username}.default/permissions.sqlite".source =
          "${config.preferences.configDirectory}/modules/nixos/desktop/firefox/permissions.sqlite";
      };

      impermanence.home.cache.directories = [
        # ".mozilla"
        ".librewolf"
      ];
    };
}
